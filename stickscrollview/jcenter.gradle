apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}


version = "0.0.1"


def siteUrl = 'https://github.com/WelliJohn/StickScrollView' // 项目的主页
def gitUrl = 'https://github.com/WelliJohn/StickScrollView.git' // Git仓库的url
group = "wellijohn.org" // 一般填你唯一的包名
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                //项目描述
                name 'ScrollViewWithStickview'
                url siteUrl
                // 开源协议
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    //填写的基本信息
                    developer {
                        id 'WelliJohn'
                        name 'WelliJohn'
                        email 'wellijohn1991@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


// 注释掉不上传源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "ScrollViewWithStickview"
        name = "ScrollViewWithStickview"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
